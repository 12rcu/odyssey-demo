
// set local resolver always return null
ext.LOCAL_ENV_RESOLVER = { key -> return null }

// load local logic
def localEnvFile = new File(
        "${System.properties['user.home']}${File.separator}.gradle",
        "env-timoliacreative.local.gradle"
)
if (localEnvFile.exists()) {
    // replaces value of LOCAL_ENV_RESOLVER closure
    apply from: localEnvFile.path
}


ext.MAVEN_REPO_URL = getEnv("MAVEN_REPO_URL")
ext.MAVEN_REPO_USER = getEnv("MAVEN_REPO_USER")
ext.MAVEN_REPO_PW = getEnv("MAVEN_REPO_PW")

String getEnv(String key) {
    // check local resolver first
    String localEnv = LOCAL_ENV_RESOLVER(key)
    if (localEnv != null && !localEnv.isBlank()) {
        return localEnv
    }

    // return system environment variable
    return System.getenv(key)
}